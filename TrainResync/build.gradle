plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = '1.5.0.b'
group = 'com.ashbill'

base {
    archivesName = 'trainresync'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.1'
}

repositories {
    maven { url = "https://maven.tterrag.com/" }               // Create deps (ok if unused)
    maven { url = "https://maven.ladysnake.org/releases" }     // MixinExtras
    maven { url = "https://repo.spongepowered.org/maven" }     // Sponge Mixin + plugin processor
    mavenCentral()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.2.0'

    implementation files("libs/create-0.5.1.j.jar")
    implementation files("libs/pantographsandwires-forge-1.20.1-beta-0.1.1.jar")

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:0.5.0")) {
        jarJar.ranged(it, "[0.5.0,)")
    }
}

mixin {
    add sourceSets.main, "trainresync.refmap.json"
}

jar {
    manifest {
        attributes([
            "Specification-Title": "trainresync",
            "Specification-Vendor": "ashbill",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor": "ashbill",
            'MixinConfigs': 'trainresync.mixins.json'
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
